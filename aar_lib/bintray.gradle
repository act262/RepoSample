apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'maven-publish'
apply plugin: 'maven'

def mavenDesc = 'android archives library sample.'
def siteUrl = 'https://github.com/act262/RepoSample'
def gitUrl = 'https://github.com/act262/RepoSample'
def issueUrl = 'https://github.com/act262/RepoSample/issues'

def licenseIds = ['Apache-2.0']
def licenseNames = ['The Apache Software License, Version 2.0']
def licenseUrls = ['http://www.apache.org/licenses/LICENSE-2.0.txt']
def inception = '2016'

def developerId = 'act262'
def developerName = 'act262'
def developerEmail = 'act262@gmail.com'

def publishedGroupId = 'com.github.act262.aar_sample'
def artifact = 'library'
def projectName = 'aar_library_sample'
def publishVersion = '0.0.6'

install {
    repositories {
        mavenInstaller {
            pom.project {
                packaging 'aar'

                // Description
                name projectName
                description mavenDesc
                url siteUrl

                // Archive
                groupId publishedGroupId
                artifactId artifact
                version publishVersion

                // License
                inceptionYear inception
                licenses {
                    licenseNames.eachWithIndex { ln, li ->
                        license {
                            name ln
                            url licenseUrls[li]
                        }
                    }
                }

                developers {
                    developer {
                        id developerId
                        name developerName
                        email developerEmail
                    }
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

// Bintray
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = projectName
        desc = mavenDesc
        websiteUrl = siteUrl
        issueTrackerUrl = issueUrl
        vcsUrl = gitUrl
        labels = ['android', 'java', 'library']
        licenses = licenseIds
        publish = true
        publicDownloadNumbers = true
    }
}
